/*
 * This project uses @Incubating APIs which are subject to change.
 */

plugins {
    id 'buildlogic.java-application-conventions'
    id 'org.graalvm.buildtools.native' version '0.11.0'
}

ext {
    jlineVersion = '3.30.5'
    picocliVersion = '4.7.7'
}

dependencies {
    implementation project(':elisp')

    implementation "org.jline:jline:${jlineVersion}"
    implementation "info.picocli:picocli:${picocliVersion}"
    annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"
    runtimeOnly "org.graalvm.polyglot:inspect-community:${truffleVersion}"
}

application {
    mainClass = 'party.iroiro.juicemacs.juice.ELispRepl'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

tasks.run.configure {
    standardInput = System.in
}

tasks.register('jvmCmd') {
    dependsOn tasks.run.taskDependencies
    dependsOn tasks.compileJava.taskDependencies

    doLast {
        var classpath = tasks.run.classpath
        var graalRuntimeArgs = graalArgs.collect { "\"${((String) it).replace("\"", "\\\"")}\"" }.join(" ")
        var cmd = "java ${graalRuntimeArgs} -cp ${classpath.asPath} ${application.mainClass.get()}"
        println(cmd)
    }
}

graalvmNative {
    toolchainDetection = true
    binaries {
        main {
            imageName = 'elisp-repl'
            mainClass = application.mainClass
            richOutput = true
            verbose = true
            buildArgs.add('--diagnostics-mode')
        }
    }
}
