/*
 * This project uses @Incubating APIs which are subject to change.
 */

plugins {
    id 'java'
    // // JaCoCo transforms the bytecode, which severely impacts how
    // // Graal/Truffle JIT compile the program.
    // // (For example, it introduces concurrency constructs to pass
    // // signals for code coverage, which Graal/Truffle will try to
    // // inline-&-compile and will eventually bail out because it is
    // // just too complex.)
    // id 'jacoco'
}

repositories {
    mavenCentral()
}

boolean isGraalVM() {
    return file("${System.getProperty("java.home")}/lib/graalvm").exists()
}
List<String> getGraalVMArgs() {
    boolean isGraalVM = isGraalVM()
    List<String> graalArgs = []
    if (!isGraalVM) {
        var classpath = configurations.create('compilerClasspath') {
            canBeResolved = true
        }
        dependencies {
            compilerClasspath "org.graalvm.compiler:compiler:${truffleVersion}"
        }
        var jars = classpath.filter { it.name.endsWith(".jar") }  // Filter out POMs
        graalArgs += [
                '-XX:+UnlockExperimentalVMOptions',
                '-XX:+EnableJVMCI',
                "--upgrade-module-path=${jars.asPath}",
        ]
    }
    return graalArgs
}
int preferredJvmVersion() {
    if (!isGraalVM()) {
        // org.graalvm.compiler:compiler does not seem to support JDK 24 yet.
        return 23
    }
    String version = System.getProperty("java.version")
    String major = version.split("\\.")[0]
    return Math.max(Integer.parseInt(major), 23)
}

ext {
    eclipseCollectionsVersion = '11.1.0'
    pmdVersion = '7.7.0'
    truffleVersion = '24.2.2'
    graalArgs = getGraalVMArgs()
    preferredJvmVersion = preferredJvmVersion()
}

dependencies {
    implementation 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.3.0'
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(preferredJvmVersion)
    }
}

//test {
//    // Always generate JaCoCo report after tests run
//    finalizedBy jacocoTestReport
//}
